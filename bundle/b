#!/usr/bin/env zsh

basename=$0
cd $HOME/.vim/bundle

bfile="bundles.txt"
ufile="bundles-unix.txt"

# read file contents into array
bundles=( ${(f)"$(<$bfile)"} )
bundles+=( ${(f)"$(<$ufile)"} )


usage() {
    print "usage: $basename [operation]\n"
    print "operations:"
    print "help   - this text"
    print "get    - git clone for all specified bundles"
    print "update - git pull for all bundles"
    print "check  - check for files not in *bundles.txt"
    print "all    - all of the above (except help)"
    print "echo   - print the bundles in *bundles.txt"
    print "\n standard is all"
}

get() {
    for i in $bundles[@]; do
        git clone $i 2>/dev/null
    done
}

update() {
    for i in *(/); do
        pushd $i
        print $i
        git pull
        print
        popd
    done
}

check() {
    for i in *(/); do
        pushd $i
        local j=$(git config --get remote.origin.url)
        [[ ${bundles[(r)$j]} == $j ]] || echo "$i ($j) not found"
        popd
    done
}

all() {
    print "Downloading missing bundles\n"  && get
    print "\nUpdating existing bundles\n"  && update
    print "\nCheck for obsolete bundles\n" && check
}

case $1 in
    "h" | "help"   ) usage  ;;
    "g" | "get"    ) get    ;;
    "u" | "update" ) update ;;
    "c" | "check"  ) check  ;;
    "a" | "all"    ) all    ;;
    "e" | "echo"   ) print ${(F)bundles} ;;
     *             ) all ;;
esac
